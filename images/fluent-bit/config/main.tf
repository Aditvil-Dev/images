locals {
  baseline_packages = ["fluent-bit-compat", "libpq"]
}

module "accts" {
  run-as = 0
  source = "../../../tflib/accts"
}

terraform {
  required_providers {
    apko = { source = "chainguard-dev/apko" }
  }
}

variable "extra_packages" {
  default     = ["fluent-bit", "fluent-bit-compat"]
  description = "The additional packages to install"
}

output "config" {
  value = jsonencode({
    "contents" : {
      // TODO: remove the need for using hardcoded local.baseline_packages by plumbing
      // these packages through var.extra_packages in all callers of this config module
      "packages" : distinct(concat(local.baseline_packages, var.extra_packages))
    },
    "entrypoint" : {
      "command" : "/usr/bin/fluent-bit"
    },
    "cmd" : "--config=/fluent-bit/etc/fluent-bit.conf",
    "accounts" : module.accts.block,
    "paths" : [
      {
        "path" : "/var/lib/fluent-bit",
        "type" : "directory",
        "uid" : 65532,
        "gid" : 65532,
        "permissions" : 493
      }
    ]
  })
}

